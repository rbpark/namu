// Apply these settings to all projects
allprojects {
        apply plugin: 'eclipse'
        apply plugin: 'idea'

        version='0.0.1'
        group = 'namu'
}

// Apply these settings to only subprojects
subprojects {
        apply plugin: 'java'
        apply plugin: 'maven-publish'
        apply plugin: 'maven'
        sourceCompatibility=JavaVersion.VERSION_1_8

	// Used to make sure eclipse doesn't write to /bin as default
        eclipse.classpath.defaultOutputDir=file('build/eclipse/classes/main')

	// Looks at maven central repository for dependencies	
	repositories {
		mavenCentral()
        }

	dependencies {
		// RelateIQ dependencies
                compile 'com.google.guava:guava:14.0.1'
                compile 'org.apache.commons:commons-lang3:3.3.2'

                testCompile 'junit:junit:4.11'
                testCompile 'org.hamcrest:hamcrest-library:1.3'
        }

	task sourcesJar(type: Jar, dependsOn: classes) {
                classifier = 'sources'
                from sourceSets.main.allSource
        }

        task javadocJar(type: Jar, dependsOn: javadoc) {
            classifier = 'javadoc'
            from javadoc.destinationDir
        }

        artifacts {
                archives sourcesJar
                archives javadocJar
        }

        publishing {
                publications {
                         mavenJava(MavenPublication) {
                                from components.java

                                artifact sourcesJar {
                                        classifier "sources"
                                }

                                artifact javadocJar {
                                        classifier "javadoc"
                                }
                        }
            }
                repositories {
                        maven {
                                credentials {
                                        username ''
                                        password ''
                                }
                                if (project.version.endsWith('-SNAPSHOT'))
                                        url ""
                                else
                                        url ""
                        }
                }
        }
}

// Used to pull the gradle wrapper
task wrapper(type: Wrapper) {
        gradleVersion = '2.2'
}
